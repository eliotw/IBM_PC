10 LOCATE 2, 1
11 CLS
12 COLOR 2,0
20 PRINT TAB(20); "  _____  ____   __  __   _____    _____ "
30 PRINT TAB(20); " |_   _||  _ \ |  \/  | |  __ \  / ____|"
40 PRINT TAB(20); "   | |  | |_) || \  / | | |__) || |     "
50 PRINT TAB(20); "   | |  |  _ < | |\/| | |  ___/ | |     "
60 PRINT TAB(20); "  _| |_ | |_) || |  | | | |     | |____ "
70 PRINT TAB(20); " |_____||____/ |_|  |_| |_|      \_____|"
80 PRINT TAB(20); ""
81 COLOR 15,0
88 PRINT TAB(34); "Team Members"
89 PRINT TAB(30); "--------------------"
90 PRINT TAB(33); "Patrick Brown"
100 PRINT TAB(32); "Aalique Grahame"
110 PRINT TAB(33); "Meghan Kaffine"
120 PRINT TAB(35); "Eliot Wong"
130 PRINT ""
140 PRINT ""
150 PRINT "1. Hi-Lo"
300 PRINT "Option: ";
310 INPUT A
320 IF A = 1 THEN 10010
330 IF A = 2 THEN 20005
400 END

10010 PRINT TAB(34); "HI LO"
10020 PRINT TAB(15); "CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
10030 PRINT: PRINT: PRINT
10100 PRINT "THIS IS THE GAME OF HI LO.": PRINT
10110 PRINT "YOU WILL HAVE 6 TRIES TO GUESS THE AMOUNT OF MONEY IN THE"
10120 PRINT "HI LO JACKPOT, WHICH IS BETWEEN 1 AND 100 DOLLARS.  IF YOU"
10130 PRINT "GUESS THE AMOUNT, YOU WIN ALL THE MONEY IN THE JACKPOT!"
10140 PRINT "THEN YOU GET ANOTHER CHANCE TO WIN MORE MONEY.  HOWEVER,"
10150 PRINT "IF YOU DO NOT GUESS THE AMOUNT, THE GAME ENDS.": PRINT
10160 R = 0
10170 B = 0: PRINT
10180 Y = INT(100 * RND(1))
10200 PRINT "YOUR GUESS";
10210 INPUT A
10220 B = B + 1
10230 IF A = Y THEN 10300
10240 IF A > Y THEN 10270
10250 PRINT "YOUR GUESS IS TOO LOW.": GOTO 10280
10270 PRINT "YOUR GUESS IS TOO HIGH."
10280 PRINT: IF B < 6 THEN 10200
10289 COLOR 4,0
10290 PRINT "YOU BLEW IT...TOO BAD...THE NUMBER WAS"; Y
10291 COLOR 15,0
10295 R = 0: GOTO 10350
10300 COLOR 18,0
10301 PRINT "GOT IT!!!!!!!!!!   YOU WIN"; Y; "DOLLARS."
10302 COLOR 15,0
10310 R = R + Y
10320 PRINT "YOUR TOTAL WINNINGS ARE NOW"; R; "DOLLARS."
10350 PRINT: PRINT "PLAY AGAIN (YES OR NO)";
10360 INPUT A$: IF A$ = "YES" THEN 10170
10380 PRINT: PRINT "SO LONG.  HOPE YOU ENJOYED YOURSELF!!!"
10390 GOTO 10

20005 PRINT TAB(32);"CHECKERS"
20010 PRINT TAB(15);"CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
20015 PRINT:PRINT:PRINT
20020 PRINT "THIS IS THE GAME OF CHECKERS.  THE COMPUTER IS X,"
20025 PRINT "AND YOU ARE O.  THE COMPUTER WILL MOVE FIRST."
20030 PRINT "SQUARES ARE REFERRED TO BY A COORDINATE SYSTEM."
20035 PRINT "(0,0) IS THE LOWER LEFT CORNER"
20040 PRINT "(0,7) IS THE UPPER LEFT CORNER"
20045 PRINT "(7,0) IS THE LOWER RIGHT CORNER"
20050 PRINT "(7,7) IS THE UPPER RIGHT CORNER"
20055 PRINT "THE COMPUTER WILL TYPE '+TO' WHEN YOU HAVE ANOTHER"
20060 PRINT "JUMP.  TYPE TWO NEGATIVE NUMBERS IF YOU CANNOT JUMP."
20065 PRINT:PRINT:PRINT
20080 DIM R(4),S(7,7):G=-1:R(0)=-99
20090 DATA 1,0,1,0,0,0,-1,0,0,1,0,0,0,-1,0,-1,15
20120 FOR X=0 TO 7:FOR Y=0 TO 7:READ J:IF J=15 THEN 20180
20160 S(X,Y)=J:GOTO 20200
20180 RESTORE:READ S(X,Y)
20200 NEXT Y,X
20230 FOR X=0 TO 7:FOR Y=0 TO 7:IF S(X,Y)>-1 THEN 20350
20310 IF S(X,Y)=-1 THEN FOR A=-1 TO 1 STEP 2:B=G:GOSUB 20650:NEXT A
20330 IF S(X,Y)=-2 THEN FOR A=-1 TO 1 STEP 2:FOR B=-1 TO 1 STEP 2:GOSUB 20650:NEXT B,A
20350 NEXT Y,X:GOTO 21140
20650 U=X+A:V=Y+B:IF U<0 OR U>7 OR V<0 OR V>7 THEN 20870
20740 IF S(U,V)=0 THEN GOSUB 20910:GOTO 20870
20770 IF S(U,V)<0 THEN 20870
20790 U=U+A:V=V+B:IF U<0 OR V<0 OR U>7 OR V>7 THEN 20870
20850 IF S(U,V)=0 THEN GOSUB 20910
20870 RETURN
20910 IF V=0 AND S(X,Y)=-1 THEN Q=Q+2
20920 IF ABS(Y-V)=2 THEN Q=Q+5
20960 IF Y=7 THEN Q=Q-2
20980 IF U=0 OR U=7 THEN Q=Q+1
21030 FOR C=-1 TO 1 STEP 2:IF U+C<0 OR U+C>7 OR V+G<0 THEN 21080
21035 IF S(U+C,V+G)<0 THEN Q=Q+1:GOTO 21080
21040 IF U-C<0 OR U-C>7 OR V-G>7 THEN 21080
21045 IF S(U+C,V+G)>0 AND(S(U-C,V-G)=0 OR(U-C=X AND V-G=Y))THEN Q=Q-2
21080 NEXT C:IF Q>R(0)THEN R(0)=Q:R(1)=X:R(2)=Y:R(3)=U:R(4)=V
21100 Q=0:RETURN
21140 IF R(0)=-99 THEN 21880
21230 PRINT CHR$(30)"FROM"R(1);R(2)"TO"R(3);R(4);:R(0)=-99
21240 IF R(4)=0 THEN S(R(3),R(4))=-2:GOTO 21420
21250 S(R(3),R(4))=S(R(1),R(2))
21310 S(R(1),R(2))=0:IF ABS(R(1)-R(3))<>2 THEN 21420
21330 S((R(1)+R(3))/2,(R(2)+R(4))/2)=0
21340 X=R(3):Y=R(4):IF S(X,Y)=-1 THEN B=-2:FOR A=-2 TO 2 STEP 4:GOSUB 21370
21350 IF S(X,Y)=-2 THEN FOR A=-2 TO 2 STEP 4:FOR B=-2 TO 2 STEP 4:GOSUB 21370:NEXT B
21360 NEXT A:IF R(0)<>-99 THEN PRINT"TO"R(3);R(4);:R(0)=-99:GOTO 21240
21365 GOTO 21420
21370 U=X+A:V=Y+B:IF U<0 OR U>7 OR V<0 OR V>7 THEN 21400
21380 IF S(U,V)=0 AND S(X+A/2,Y+B/2)>0 THEN GOSUB 20910
21400 RETURN
21420 PRINT:PRINT:PRINT:FOR Y=7 TO 0 STEP-1:FOR X=0 TO 7:I=5*X:PRINT TAB(I);
21430 IF S(X,Y)=0 THEN PRINT".";
21470 IF S(X,Y)=1 THEN PRINT"O";
21490 IF S(X,Y)=-1 THEN PRINT"X";
21510 IF S(X,Y)=-2 THEN PRINT"X*";
21530 IF S(X,Y)=2 THEN PRINT"O*";
21550 NEXT X:PRINT" ":PRINT:NEXT Y:PRINT
21552 FOR L=0 TO 7
21554 FOR M=0 TO 7
21556 IF S(L,M)=1 OR S(L,M)=2 THEN Z=1
21558 IF S(L,M)=-1 OR S(L,M)=-2 THEN T=1
21560 NEXT M
21562 NEXT L
21564 IF Z<>1 THEN 21885
21566 IF T<>1 THEN 21880
21570 Z=0: T=0
21590 INPUT "FROM";E,H:X=E:Y=H:IF S(X,Y)<=0 THEN 21590
21670 INPUT "TO";A,B:X=A:Y=B
21680 IF S(X,Y)=0 AND ABS(A-E)<=2 AND ABS(A-E)=ABS(B-H)THEN 21700
21690 PRINT CHR$(7)CHR$(11);:GOTO 21670
21700 I=46
21750 S(A,B)=S(E,H):S(E,H)=0:IF ABS(E-A)<>2 THEN 21810
21800 S((E+A)/2,(H+B)/2)=0
21802 INPUT "+TO";A1,B1:IF A1<0 THEN 21810
21804 IF S(A1,B1)<>0 OR ABS(A1-A)<>2 OR ABS(B1-B)<>2 THEN 21802
21806 E=A:H=B:A=A1:B=B1:I=I+15:GOTO 21750
21810 IF B=7 THEN S(A,B)=2
21830 GOTO 20230
21880 PRINT: PRINT "YOU WIN.": END
21885 PRINT: PRINT "I WIN.": END
